# Produced by CVXGEN, 2018-04-10 14:30:30 -0400.
# CVXGEN is Copyright (C) 2006-2017 Jacob Mattingley, jem@cvxgen.com.
# The code in this file is Copyright (C) 2006-2017 Jacob Mattingley.
# CVXGEN, or solvers produced by CVXGEN, cannot be used for commercial
# applications without prior written permission from Jacob Mattingley.

# Filename: description.cvxgen.
# Description: A description of the CVXGEN problem.

dimensions
  m = 3  # inputs
  n = 6  # states
  T = 15 # receding horizon
end

parameters
  A (n,n) {1,1 1,2 2,2 3,3 3,4 4,4 5,5 5,6 6,6 }# dynamics matrix
  B (n,m) {1,1 2,1 3,2 4,2 5,3 6,3}# transfer matrix
  Q  (n,n) diagonal psd # terminal state cost
  R (m,m) diagonal psd # stage input cost
  x[0] (n) # initial state condition
  xN (n) # terminal state condition
  x_max (n) # state (position and velocity) upper bound
  x_min (n) # state (position and velocity) lower bound
  u_max (m) # input upper bound
  u_min (m) # input lower bound
  g (m) # affine term to describe gravity
  f_obs[t] (m), t=0..T # obstacle's potential
end

variables
  x[t] (n), t=1..T+1 # state
  u[t] (m), t=0..T # input
end

minimize
  sum[t=0..T](quad(u[t] + g + f_obs[t], R)) + quad(x[T+1]-xN, Q)
subject to
  x[t+1] == A*x[t] + B*(u[t] + g + f_obs[t]), t=0..T  # dynamics constraints
  x_min <= x[t] <= x_max, t=1..T+1 # state constraint
  u_min <= u[t] <= u_max, t=0..T  # input constraint
end
